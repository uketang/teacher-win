; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "优课堂教师端"
!define PRODUCT_COMPAT_NAME "优课堂教师端"
!define PRODUCT_SHORT_NAME "teacher"
!define PRODUCT_VERSION "1.0"
!define PRODUCT_PATCH_VERSION "1.0.0"
!define PRODUCT_PUBLISHER "uketang Co., Ltd."
!define PRODUCT_WEB_SITE "http://uketang.com"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\uketang.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PNAME_WITH_VERSION}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PNAME_WITH_VERSION "${PRODUCT_NAME} ${PRODUCT_VERSION}"

SetCompressor bzip2

; MUI 1.67 compatible ------
!include "MUI.nsh"
!include "nsDialogs.nsh"
!include "LogicLib.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "install.ico"
!define MUI_UNICON "uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "license\${PRODUCT_SHORT_NAME}-${PRODUCT_VERSION}-CN.rtf"
; Components page
!insertmacro MUI_PAGE_COMPONENTS
; Service port page
;Page custom nsDialogsPage nsDialogsPageLeave "--配置UMS服务"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "SimpChinese"

; MUI end ------

Name "${PNAME_WITH_VERSION}"
OutFile "${PRODUCT_COMPAT_NAME}${PRODUCT_PATCH_VERSION}(CN)安装程序.exe"
InstallDir "$PROGRAMFILES\uketang\${PRODUCT_SHORT_NAME}-${PRODUCT_VERSION}"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show
XPStyle on


;Hidden section
Section "-优课堂教师端主程序" SEC01
	SetOutPath "$INSTDIR"
	SetOverwrite try
	File "docs\${PRODUCT_COMPAT_NAME}使用手册.pdf"
	File /r "..\electron\*"
	File /r "..\dist\node_modules"
	File /r "..\dist\static"
	File /r "..\dist\ico"
	File "..\dist\index.html"
	File "..\dist\main.js"
	File "..\dist\package.json"
SectionEnd

Section -AdditionalIcons
	SetShellVarContext all # 解决Vista及以上版本快捷方式删不掉的问题
	
	SetOutPath $INSTDIR
	WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
	CreateDirectory "$SMPROGRAMS\${PNAME_WITH_VERSION}"
	CreateShortCut "$SMPROGRAMS\${PNAME_WITH_VERSION}\优课堂.lnk" "$INSTDIR\uketang.exe" '"$INSTDIR"'
	CreateShortCut "$DESKTOP\优课堂.lnk" "$INSTDIR\uketang.exe" '"$INSTDIR"'
	CreateShortCut "$SMPROGRAMS\${PNAME_WITH_VERSION}\优课堂网站.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
	CreateShortCut "$SMPROGRAMS\${PNAME_WITH_VERSION}\${PRODUCT_COMPAT_NAME}使用手册.lnk" "$INSTDIR\${PRODUCT_COMPAT_NAME}使用手册.pdf"
	CreateShortCut "$SMPROGRAMS\${PNAME_WITH_VERSION}\卸载.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
	WriteUninstaller "$INSTDIR\uninst.exe"
	WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_DIR_REGKEY}" ""                 "$INSTDIR\uketang.exe"
	WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName"      "$(^Name)"
	WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString"  "$INSTDIR\uninst.exe"
	WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon"      "$INSTDIR\uketang.exe"
	WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion"   "${PRODUCT_VERSION}"
	WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout"     "${PRODUCT_WEB_SITE}"
	WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher"        "${PRODUCT_PUBLISHER}"
	WriteRegStr "HKLM" "SYSTEM\CurrentControlSet\Services\SharedAccess\Parameters\FirewallPolicy\StandardProfile\AuthorizedApplications\List" "$INSTDIR\uketang.exe" "$INSTDIR\uketang.exe:*:Enabled:uketang for teacher ${PRODUCT_VERSION}"
SectionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
	!insertmacro MUI_DESCRIPTION_TEXT ${SEC01} "优课堂 - 教师端."
!insertmacro MUI_FUNCTION_DESCRIPTION_END


Function .onInstSuccess
	HideWindow
	MessageBox MB_ICONINFORMATION|MB_YESNO|MB_DEFBUTTON1 "恭喜您，$(^Name) 已成功地完成安装！您要现在查看使用手册吗？" IDYES +2
	Abort
	SetOutPath "$INSTDIR"
	nsExec::ExecToStack 'cmd /c "$INSTDIR\${PRODUCT_COMPAT_NAME}使用手册.pdf"'
FunctionEnd

Function un.onUninstSuccess
	HideWindow
	MessageBox MB_ICONINFORMATION|MB_YESNO|MB_DEFBUTTON2 "$(^Name) 已成功地从您的计算机移除。是否保留用户数据？" IDYES +2
	RMDir "$INSTDIR\data"
FunctionEnd

Function un.onInit
	MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "您确实要完全移除 $(^Name) ，及其所有的组件？" IDYES +2
	Abort
FunctionEnd

Section Uninstall
	SetShellVarContext all # 解决Vista及以上版本快捷方式删不掉的问题
	
	SetOutPath "$INSTDIR"
	RMDir /r /REBOOTOK "$INSTDIR\node_modules"
	RMDir /r /REBOOTOK "$INSTDIR\static"
	RMDir /r /REBOOTOK "$INSTDIR\locales"
	RMDir /r /REBOOTOK "$INSTDIR\swiftshader"
	RMDir /r /REBOOTOK "$INSTDIR\resources"
	RMDir /r /REBOOTOK "$INSTDIR\ico"
	Delete "$INSTDIR\chrome_100_percent.pak"
	Delete "$INSTDIR\libGLESv2.dll"
	Delete "$INSTDIR\resources.pak"
	Delete "$INSTDIR\chrome_200_percent.pak"
	Delete "$INSTDIR\LICENSE"
	Delete "$INSTDIR\snapshot_blob.bin"
	Delete "$INSTDIR\d3dcompiler_47.dll"
	Delete "$INSTDIR\LICENSES.chromium.html"
	Delete "$INSTDIR\ffmpeg.dll"
	Delete "$INSTDIR\v8_context_snapshot.bin"
	Delete "$INSTDIR\icudtl.dat"
	Delete "$INSTDIR\natives_blob.bin"
	Delete "$INSTDIR\version"
	Delete "$INSTDIR\libEGL.dll"
	Delete "$INSTDIR\uketang.exe"
		
	Delete "$INSTDIR\main.js"
	Delete "$INSTDIR\index.html"
	Delete "$INSTDIR\package.json"
	
	Delete "$INSTDIR\uninst.exe"

	; delete shortcut in install dir
	Delete "$INSTDIR\${PRODUCT_NAME}.url"
	; delete document
	Delete "$INSTDIR\${PRODUCT_COMPAT_NAME}使用手册.pdf"
	; delete shortcut in program folder
	Delete "$SMPROGRAMS\${PNAME_WITH_VERSION}\${PRODUCT_COMPAT_NAME}使用手册.lnk"

	Delete "$DESKTOP\优课堂.lnk"
	Delete "$SMPROGRAMS\${PNAME_WITH_VERSION}\优课堂.lnk"
	Delete "$SMPROGRAMS\${PNAME_WITH_VERSION}\优课堂网站.lnk"
	Delete "$SMPROGRAMS\${PNAME_WITH_VERSION}\卸载.lnk"
	RMDir "$SMPROGRAMS\${PNAME_WITH_VERSION}"

	DeleteRegValue "HKLM" "SYSTEM\CurrentControlSet\Services\SharedAccess\Parameters\FirewallPolicy\StandardProfile\AuthorizedApplications\List" "$INSTDIR\uketang.exe"
	DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
	DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
	SetAutoClose true
SectionEnd